use std::str::FromStr;

use crate::ast::{Expression, Expr, OpCode, UnCode, ExpressionTransform};

grammar;

match {
    r"\s*" => { }, // The default whitespace skipping is disabled an `ignore pattern` is specified
    r"#[^\n\r]*[\n\r]*" => { }, // Skip `# comments`
    _
}

pub Expression: Box<Expression> = Expr00;

Expr00: Box<Expression> = {
    <l:@L> <left: Expression> <operation: Op10> <right: Expr10> <r:@R> => Box::new(Expression { expression: Expr::BinaryOperation { left, right, operation }, location: (l, r) }),
    Expr10,
};

Expr10: Box<Expression> = {
    <l:@L> <left: Expr10> <operation: Op20> <right: Expr20> <r:@R> => Box::new(Expression { expression: Expr::BinaryOperation { left, right, operation }, location: (l, r) }),
    Expr20,
};

Expr20: Box<Expression> = {
    <l:@L> <left: Expr20> <operation: Op30> <right: Expr30> <r:@R> => Box::new(Expression { expression: Expr::BinaryOperation { left, right, operation }, location: (l, r) }),
    Expr30,
};

Expr30: Box<Expression> = {
    <l:@L> <left: Expr30> <operation: Op40> <right: Expr40> <r:@R> => Box::new(Expression { expression: Expr::BinaryOperation { left, right, operation }, location: (l, r) }),
    Expr40,
};

Expr40: Box<Expression> = {
    <l:@L> <left: Expr40> <operation: Op50> <right: Expr50> <r:@R> => Box::new(Expression { expression: Expr::BinaryOperation { left, right, operation }, location: (l, r) }),
    Expr41,
};

Expr41: Box<Expression> = {
    <l:@L> <operation: Un10> <right: Expr50> <r:@R> => Box::new(Expression { expression: Expr::UnaryOperation { right, operation }, location: (l, r) }),
    Expr50,
};

Expr50: Box<Expression> = {
    <l:@L> <left: Expr50> "?" <truthy: Expr60> ":" <falsy: Expr60> <r:@R> => Box::new(Expression { expression: Expr::Conditional {left, truthy, falsy}, location: (l, r) }),
    Expr51,
};

Expr51: Box<Expression> = {
    <l:@L> "if" <left: Expr51> "{" <truthy: Expr60> "}" "else" "{" <falsy: Expr60> "}" <r:@R> => Box::new(Expression { expression: Expr::Conditional {left, truthy, falsy}, location: (l, r) }),
    Expr60,
};

Expr60: Box<Expression> = {
    <l:@L> <subject: Expr60> "|" <name: Identifier> <args: Args?> <r:@R> => Box::new(Expression { expression: Expr::Transform{name, subject, args}, location: (l, r) }),
    Expr70
};

Expr70: Box<Expression> = {
    <l:@L> <subject: Expr60>  "|" "mapTransform(" <name: Identifier> <args: Args?> ")" <r:@R> => Box::new(Expression { expression: Expr::MapTransform {name, subject, args }, location: (l, r) }),
    Expr80
}

Expr80: Box<Expression> = {
    <l:@L> <subject: Expr60> "|" "filterTransform(" <name: Identifier> <args: Args?> ")" <r:@R> => Box::new(Expression { expression: Expr::FilterTransform {name, subject, args }, location: (l, r) }),
    Expr81
}

Expr81: Box<Expression> = {
    <l:@L> <subject: Expr60>  "|" <name: ET> "(" <expression: Expression> ","? <args: Number?> ")" <r:@R> => Box::new(Expression { expression: Expr::ExpressionTransform {name, subject, expression, args }, location: (l, r) }),
    Expr82
}

Expr82: Box<Expression> = {
    <l:@L> <subject: Expr60> "|" "reduce(" <init: Expression> "," <expression: Expression> ")" <r:@R> => Box::new(Expression { expression: Expr::ReduceExpression { subject, init, expression }, location: (l, r) }),
    Expr85
}

Expr85: Box<Expression> = {
    <l:@L> "date(" <date: Expr85> "," <format: Expression> ")" <r:@R> => Box::new(Expression { expression: Expr::Date { date, format }, location: (l, r) }),
    Expr86
}

Expr86: Box<Expression> = {
    <l:@L> "datetime(" <datetime: Expr80> "," <format: Expression> ")" <r:@R> => Box::new(Expression { expression: Expr::DateTime { datetime, format }, location: (l, r) }),
    Expr87
}

Expr87: Box<Expression> = {
    <l:@L> "duration(" <duration: Expr80> "," <duration_type: Expression> ")" <r:@R> => Box::new(Expression { expression: Expr::Duration { duration, duration_type }, location: (l, r) }),
    Expr88
}

Expr88: Box<Expression> = {
    <l:@L> "$now" <r:@R> => Box::new(Expression { expression: Expr::Now, location: (l, r) }),
    Expr90
}

Expr90: Box<Expression> = {
    <l:@L> <subject: Expr70> <index: Index> <r:@R> => Box::new(Expression { expression: Expr::IndexOperation{subject, index}, location: (l, r) }),
    <l:@L> <subject: Expr70> "." <ident: Identifier> <r:@R> => Box::new(Expression { expression: Expr::DotOperation{subject, ident}, location: (l, r) }),
    Expr100
};

Expr100: Box<Expression> = {
    <l:@L> <e:Number> <r:@R> => Box::new(Expression { expression: Expr::Number(e), location: (l, r) }),
    <l:@L> <e:Boolean> <r:@R> => Box::new(Expression { expression: Expr::Boolean(e), location: (l, r) }),
    <l:@L> <e:String> <r:@R> => Box::new(Expression { expression: Expr::String(e), location: (l, r) }),
    <l:@L> <e:Array> <r:@R> => Box::new(Expression { expression: Expr::Array(e), location: (l, r) }),
    <l:@L> <e:Object> <r:@R> => Box::new(Expression { expression: Expr::Object(e), location: (l, r) }),
    <l:@L> <e:Identifier> <r:@R> => Box::new(Expression { expression: Expr::Identifier(e), location: (l, r) }),
    <l:@L> <e:Regex> <r:@R> => Box::new(Expression { expression: Expr::Regex(e), location: (l, r) }),
    "(" <Expression> ")",
};

Args: Vec<Box<Expression>> = {
    "(" <Comma<Expression>> ")"
};

Un10: UnCode = {
    "!" => UnCode::Not,
    "-" => UnCode::Minus,
    "+" => UnCode::Plus,
}

Op10: OpCode = {
    "&&" => OpCode::And,
    "||" => OpCode::Or,
};

Op20: OpCode = {
    "==" => OpCode::Equal,
    "!=" => OpCode::NotEqual,
    ">=" => OpCode::GreaterEqual,
    "<=" => OpCode::LessEqual,
    ">" => OpCode::Greater,
    "<" => OpCode::Less,
    "in" => OpCode::In,
    "~" => OpCode::Matches,
    "@" => OpCode::Capture,
    "@+" => OpCode::CaptureMultiple,
};

Op30: OpCode = {
    "+" => OpCode::Add,
    "-" => OpCode::Subtract,
};

Op40: OpCode = {
    "*" => OpCode::Multiply,
    "//" => OpCode::FloorDivide,
    "/" => OpCode::Divide,
};

Op50: OpCode = {
    "%" => OpCode::Modulus,
    "^" => OpCode::Exponent,
};

ET: ExpressionTransform = {
    "map" => ExpressionTransform::Map,
    "filter" => ExpressionTransform::Filter,
    "sortBy" => ExpressionTransform::SortBy,
    "any" => ExpressionTransform::Any,
    "all" => ExpressionTransform::All,
    "find" => ExpressionTransform::Find,
    "findIndex" => ExpressionTransform::FindIndex,
    "apply" => ExpressionTransform::Apply,
};

Number: f64 = {
    r"-?[0-9]+" => f64::from_str(<>).unwrap(),
    r"-?[0-9]+\.[0-9]*" => f64::from_str(<>).unwrap(),
    r"-?\.[0-9]+" => f64::from_str(<>).unwrap(),
};

String: String = {
    <s: r#""([^"\\]*(\\")?)*""#> => s[1..s.len() - 1].to_string().replace("\\\"", "\""),
    <s: r#"'([^'\\]*(\\')?)*'"#> => s[1..s.len() - 1].to_string().replace("\\'", "'"),
};

Regex: String = {
    <s: r#"/([^']*(\\')?)*/"#> => s[1..s.len() - 1].to_string(),
}

Identifier: String = {
    r#"[a-zA-Z_][a-zA-Z0-9_]*"# => <>.to_string()
}

Index: Box<Expression> = {
    <l:@L> "[" "." <ident: Identifier> <op: Op20> <right: Expr80> "]" <r:@R> => Box::new(Expression { expression: Expr::Filter {ident, op, right}, location: (l, r) }),
    "[" <Expression> "]",
}

Boolean: bool = {
    "true" => true,
    "false" => false,
}

Comma<T>: Vec<T> = {
    <v: (<T> ",")*> <e:T?> => match e {
        None => v,
        Some(e) => {
            let mut v = v;
            v.push(e);
            v
        }
    }
};

Array: Vec<Box<Expression>> = {
    "[" <Comma<Expression>> "]"
}

Object: Vec<(String, Box<Expression>)> = {
    "{" <Comma<(<ObjectIdentifier> ":" <Expression>)>> "}",
    "{}" => vec![],
}

ObjectIdentifier: String = {
    String, 
    Identifier
}
